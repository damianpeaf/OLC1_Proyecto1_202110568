package OLCCompiler;
import OLCCompiler.Set.*;
import OLCCompiler.Tree.*;
import OLCCompiler.Error.*;
import java_cup.runtime.*;
import java.util.ArrayList;

class OLCParser;

/* -- CUSTOM CODE FOR PARSER -- */

parser code {:

    public ErrorTable errorTable = new ErrorTable();
    public SetsTable setsTable = new SetsTable();
    public ArrayList<RegexTree> regexTrees = new ArrayList<RegexTree>();
    public ArrayList<Evaluation> evaluationStrings = new ArrayList<Evaluation>();

    private int nodeCounter = 1;
    private int operatorCounter = 0;


   public void syntax_error(Symbol s) {

      if(s.sym == OLCParserSym.LEXICAL_ERROR){
        errorTable.add(new OLCError(ErrorType.LEXIC, s));
      }else{
        errorTable.add(new OLCError(ErrorType.SYNTAX, s));
      }
   }

:};

/* -- TERMINAL SYMBOLS -- */

terminal SET_DECLARATION,
         IDENTIFIER,
         LBRACE,
         RBRACE,
         COLON,
         SEMICOLON,
         ARROW,
         SCOPE_BREAK,
         STRING_LITERAL,
         TILDE,
         COMMA,
         LOWERCASE,
         UPPERCASE,
         ASCII,
         AND,
         OR,
         KLEENE,
         PLUS,
         QUESTION,
         WORD,
         ESCAPED_LINEBREAK,
         ESCAPED_SINGLE_QUOTE,
         ESCAPED_DOUBLE_QUOTE,
         LEXICAL_ERROR;

terminal Integer DIGIT;


/* -- NON-TERMINAL SYMBOLS -- */

non terminal program,
             scopes,
             decl_scope,
             decl,
             decls,
             set_decl,
             regex_decl,
             regex_expr,
             regex_exprs,
             stmt_scope,
             eval_stmt,
             eval_stmts,
             escaped_sequence;

// SET DECLARATION
nonterminal Set set;
nonterminal Set compr_set;
nonterminal Set extend_set;
nonterminal SetElement set_element;
nonterminal ArrayList<SetElement> set_elements;
nonterminal String set_refence;

// REGEX
nonterminal Node regex_terminal;
nonterminal Node regex_term;



/* -- GRAMATICAL RULES -- */

// INITIAL RULES
program ::= LBRACE scopes RBRACE;

scopes ::= decl_scope SCOPE_BREAK SCOPE_BREAK stmt_scope;
escaped_sequence ::= ESCAPED_LINEBREAK
                   | ESCAPED_SINGLE_QUOTE
                   | ESCAPED_DOUBLE_QUOTE;

// DECLARATION SCOPE

decl_scope ::= decl decls;
decl ::= set_decl | regex_decl;
decls ::= decl decls | ;

// SET DECLARATION
set_decl ::= SET_DECLARATION COLON IDENTIFIER:sn ARROW set:s SEMICOLON {: s.setName(String.valueOf(sn)); setsTable.add(s); :};
set ::= compr_set:s {: RESULT = s; :}
      | extend_set:s {: RESULT = s; :} ;

compr_set ::= set_element:e1 TILDE set_element:e2 {:
    Set set = new ComprehensionSet();
    ArrayList<SetElement> elements = new ArrayList<SetElement>();
    elements.add(e1);
    elements.add(e2);
    set.addElements(elements);
    RESULT = set;
 :};

set_element ::= ASCII :e {: RESULT = new SetElement(SetElementType.ASCII, String.valueOf(e));  :}
              | LOWERCASE:e {: RESULT = new SetElement(SetElementType.LOWERCASE, String.valueOf(e));  :}
              | UPPERCASE:e {: RESULT = new SetElement(SetElementType.UPPERCASE, String.valueOf(e));  :}
              | DIGIT:e {: RESULT = new SetElement(SetElementType.DIGIT, String.valueOf(e));  :};

extend_set ::= set_element:e1 set_elements:es {:
    ArrayList<SetElement> elements = new ArrayList<SetElement>();
    elements.add(e1);

    if(es != null){
        es.forEach((e) -> elements.add(e));
    }

    Set set = new ExtensionSet();
    set.addElements(elements);
    RESULT = set;
:};
set_elements ::= COMMA set_element:e1 set_elements:es {:
    ArrayList<SetElement> elements = new ArrayList<SetElement>();
    elements.add(e1);
    if(es != null){
        es.forEach((e) -> elements.add(e));
    }
    RESULT = elements;
:}
| ;


// REGEX DECLARATION
regex_decl ::= IDENTIFIER:na ARROW regex_term:no SEMICOLON  {:

  Node acceptNode = new Node(nodeCounter++, NodeType.NODE_ACCEPT, "#");
  Node rootNode = new Node(operatorCounter++, NodeType.NODE_AND, ".", no, acceptNode);
  RegexTree tree = new RegexTree(String.valueOf(na), rootNode);
  regexTrees.add(tree);
  nodeCounter = 0;
  operatorCounter = 0;

:};
regex_term ::= AND regex_term:nl regex_term:nr {: RESULT = new Node(operatorCounter++, NodeType.NODE_AND, ".", nl, nr); :}
             | OR regex_term:nl regex_term:nr {: RESULT = new Node(operatorCounter++, NodeType.NODE_OR, "|", nl, nr); :}
             | KLEENE regex_term:n {: RESULT = new Node(operatorCounter++, NodeType.NODE_KLEENE, "*", n); :}
             | PLUS regex_term:n {: RESULT = new Node(operatorCounter++, NodeType.NODE_PLUS, "+", n); :}
             | QUESTION regex_term:n {: RESULT = new Node(operatorCounter++, NodeType.NODE_OPTIONAL,"?", n); :}
             | regex_terminal:n {: RESULT = n; :};
regex_terminal ::= WORD:t {: RESULT = new Node(nodeCounter++, NodeType.NODE_I, String.valueOf(t)); :}
                 | DIGIT:t {: RESULT = new Node(nodeCounter++, NodeType.NODE_I, String.valueOf(t)); :}
                 | STRING_LITERAL:t {: RESULT = new Node(nodeCounter++, NodeType.NODE_I, String.valueOf(t)); :}
                 | escaped_sequence:t {: RESULT = new Node(nodeCounter++, NodeType.NODE_I, String.valueOf(t)); :}
                 | set_refence:n {: RESULT = new Node(nodeCounter++, NodeType.NODE_I, new SetReference(n, setsTable)); :};

set_refence ::= LBRACE IDENTIFIER:n RBRACE {: RESULT = String.valueOf(n); :};

// STATEMENT SCOPE
stmt_scope ::= eval_stmt eval_stmts;
eval_stmt ::= IDENTIFIER:n COLON STRING_LITERAL:s SEMICOLON {: evaluationStrings.add(new Evaluation(String.valueOf(n), String.valueOf(s)));  :};
eval_stmts ::= eval_stmt eval_stmts | ;

